newgui main [
    guilist [
        guitext "name: " (playermodelicon)
        newname = (getname)
        guifield newname 15 [name $newname]
    ]
    guibutton (concat "model:" (playermodelname)) [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel] (playermodelicon)
    guilist [
        guibutton "crosshair: " [showgui crosshair] (playermodelicon)
        guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guibar
    guibutton "server browser.."  "showgui servers"
    if (isconnected) [
        guibar
        if (|| $editing (m_edit (getmode))) [
            guibutton "editing.." "showgui editing"
        ]
        guibutton "vote game mode / map.." "showgui gamemode"
        // guibutton "lanconnect"
        // guibutton "connect localhost"
        guibutton "switch team" [if (strcmp (getteam) "good") [team evil] [team good]]
        guibutton "toggle spectator" [spectator (! (isspectator (getclientnum)))] "spectator"
        guibutton "master.." [showgui master]
        guibar
        guibutton "wc settings.."    "f_show_wc_settings"
        guibar
        guibutton "options.."  "showgui options"
        guibutton "disconnect" "disconnect"         "exit"
    ] [
        guibutton "bot match.."      "showgui botmatch"
        guibutton "campaign.."       "showgui campaign"
        guibar
        guibutton "wc settings.."    "f_show_wc_settings"
        guibar
        guibutton "options.."        "showgui options"
        guibutton "about.."          "showgui about"
        guibutton "quit"             "quit"                 "exit"
    ]
] 0


if ( = (checkwc) 1) [

newgui "geoip database update" [
    local total
    local downloaded
    local time

    total = (divf $geoip_update_totalsize 1024)
    downloaded = (divf $geoip_update_downloaded 1024)
    time = (divf $geoip_update_time_elapsed 1000)

    guitext "Downloading geoip database..."

    guibar

    guitext (format "Size: %1 kb" (roundf $total 2))
    guitext (format "Downloaded: %1 kb (%2%%)" (roundf $downloaded 2) $geoip_update_percent_done)
    guitext (format "Time Elapsed: %1 seconds" (roundf $time 2))

    guibar

    guialign 0 [
        guibutton "close" [hidegui "geoip database update"]
    ]
]

geoip_download_status_update = [
    geoip_update_totalsize = $arg1
    geoip_update_downloaded = $arg2
    geoip_update_percent_done = $arg3
    geoip_update_time_elapsed = $arg4
]

show_geoip_download_status_gui = [
    showgui "geoip database update"
]

// GUI extensions

createine "showserverfilteroptions" 0

createine "filteroomod" 0
createine "filterhopmod" 0
createine "filteranymod" 1

updateservermodfiltervars = [
    filteroomod = $arg1
    filterhopmod = $arg2
    filteranymod = $arg3
    filterservermod $arg4
]

f_filteroomod = [ if ($filteroomod) [ updateservermodfiltervars 1 0 0 -3 ] [ f_filteranymod ] ]
f_filterhopmod = [ updateservermodfiltervars 0 1 0 -2 ]
f_filteranymod = [ updateservermodfiltervars 0 0 1 0 ]

serverfilter = [
    guibar

    guilist [
        guitext "server description " 0
        guifield filterserverdesc 15 []
        guibar
        guitext "game mode " 0
        guifield filterservermodestring 15 []
        guibar
        guitext "country " 0
        guifield filterservercountry 2 []
        guibar
        guitext "server mod" 0
        guibar
        guiradio "oo|mod" filteroomod 1 $f_filteroomod
        guibar
        guiradio "hopmod" filterhopmod 1 $f_filterhopmod
        guibar
        guiradio "any" filteranymod 1 $f_filteranymod
    ]
]

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "update from master server" "updatefrommaster"
                guibar
                guibutton "find players" [showgui $extinfo_player_search_guiname]
                guibar
                guicheckbox "oo|servers" filteroomod 1 0 $f_filteroomod
                guibar
                guibar
                guicheckbox "search LAN" searchlan
                guibar
                guicheckbox "auto-update" autoupdateservers
                guibar
                guicheckbox "auto-sort" autosortservers
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort" "sortservers"
                ]
                guibar
                guicheckbox "show filter options" showserverfilteroptions
            ]
            if (= (checkwc) 1) [
                if ($showserverfilteroptions) [ serverfilter ]
            ]
            guibar
        ] 17
    ]
] "" [initservers]

]

f_show_wc_settings = [
    if (= (checkwc) 1) [
        showgui "wc settings 1"
    ] [
        showgui "WCError"
    ]
]

newgui "WCError" [
    guitext "^f3ERROR:" 0
    guitext "You're not using WC." 0
    guitext "Get it at http://wc-ng.sauerworld.org" 0
]

newgui "wc settings 1" [
    f_show_wcmenu
]
