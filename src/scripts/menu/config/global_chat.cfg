///////////////////////////////////////////////////////
// Global Chat Gui
///////////////////////////////////////////////////////

if (= (checkwcng) 1) [

process_gchat_clients = [

    gchat_index = 0

    scriptvector gchat_client_names
    scriptvector gchat_client_ids
    scriptvector gchat_client_sec_connected

    if ($showcountry) [
        scriptvector gchat_client_country_names
        scriptvector gchat_client_country_codes
        scriptvector gchat_server_country_names
        scriptvector gchat_server_country_codes
    ]

    scriptvector gchat_server_names
    scriptvector gchat_server_sec_connected
    scriptvector gchat_server_addresses

    // name id sec-connected country countrycode
    // servername cn sec-connected servercountry servercountrycode hostname host(int) port

    local callbackargs
    callbackargs = "client_name client_id client_sec_connected client_country client_country_code"
    callbackargs = (concat $callbackargs "server_name ignore server_sec_connected server_country")
    callbackargs = (concat $callbackargs "server_country_code server_ip ignore server_port")

    wcchatloopclients $callbackargs [
        server_address = (format "%1 %2" $server_ip $server_port)

        scriptvector gchat_client_names $gchat_index $client_name
        scriptvector gchat_client_ids $gchat_index $client_id
        scriptvector gchat_client_sec_connected $gchat_index $client_sec_connected

        if ($showcountry) [
            scriptvector gchat_client_country_names $gchat_index (? (=s $client_country "") "??" $client_country)
            scriptvector gchat_client_country_codes $gchat_index (? (=s $client_country_code "") "??" $client_country_code)
            scriptvector gchat_server_country_names $gchat_index (? (=s $server_country "") "??" $server_country)
            scriptvector gchat_server_country_codes $gchat_index (? (=s $server_country_code "") "??" $server_country_code)
        ]

        scriptvector gchat_server_names $gchat_index (? (=s $server_name "") "- unnamed server -" $server_name)
        scriptvector gchat_server_sec_connected $gchat_index $server_sec_connected
        scriptvector gchat_server_addresses $gchat_index $server_address

        gchat_index = (+ $gchat_index 1)
    ]
]

gui_render_country = [
    local desc
    local x
    local end
    local sv_country_codes
    local sv_country_names

    desc = $arg1
    x = $arg2
    end = $arg3
    sv_country_codes = $arg4
    sv_country_names = $arg5

    if ($showcountry) [
        guilist [
            guititleleftalign $desc
            while [(< $x $end)] [
                local icon
                local countrycode
                local country

                icon = 0
                countrycode = (scriptvector $sv_country_codes $x)

                if (< $showcountry 5) [
                    if (> (mod $showcountry 2) 0) [
                        country = $countrycode
                    ] [
                        country = (scriptvector $sv_country_names $x)
                    ]
                ]

                if (>= $showcountry 3) [
                    icon = (countryflagpath $countrycode)
                    if (=s $icon "") [ icon = "UNKNOWN.png" ]
                ]

                guistayopen [
                    guitext $country $icon
                ]

                x = (+ $x 1)
            ]

            guistrut (? (! (mod $showcountry 2)) 16 8) 1
        ]
    ]
]

gui_render_server = [
    local desc
    local x
    local end
    local sv_server_names
    local sv_server_addresses
    local port_offset

    desc = $arg1
    x = $arg2
    end = $arg3
    sv_server_names = $arg4
    sv_server_addresses = $arg5
    port_offset = $arg6

    local address
    local ipaddr
    local port

    guilist [
        guititleleftalign $desc
        x = 0
        while [(< $x $end)] [
            address = (scriptvector $sv_server_addresses $x)
            if (! (=s $address "0.0.0.0 0")) [
                ip = (at $address 0)
                port = (at $address 1)
                guibutton (scriptvector $sv_server_names $x) [
                    if ($extinfoserverbrowser) [
                        extinfoconnect @@ip (- @@port @@port_offset)
                    ] [
                        connect @@ip (- @@port @port_offset)
                    ]
                ] 0
            ] [
               guitext "- not playing -" 0
            ]
            x = (+ $x 1)
        ]
        guistrut 27 1
    ]
]

render_gchat_clients = [
    local x

    guilist [
    guilist [
    guilist [

        guilist [
            guititleleftalign "name"
            local cname
            x = 0
            while [(< $x $gchat_index)] [
                cname = (scriptvector gchat_client_names $x)
                cname = (format "%1 (%2)" $cname (scriptvector gchat_client_ids $x))
                guitext $cname 0
                x = (+ $x 1)
            ]
            guistrut 15 1
        ]

        gui_render_country "country" 0 $gchat_index gchat_client_country_codes gchat_client_country_names
        gui_render_server "server" 0 $gchat_index gchat_server_names gchat_server_addresses 0
        //gui_render_country "server country" 0 $gchat_index gchat_server_country_codes gchat_server_country_names

    ]
    ]
    ]
]

gchat_gui_name = "Global Chat"

newgui $gchat_gui_name [
    guistayopen [

        if ($wcchatisconnected) [
            local text
            text = (format "Disconnect (Latency: %2 ms)", (roundf (wcchatlatency 1) 2))
            guibutton $text wcchatdisconnect
        ] [
            guibutton "Connect" [wcchatdisconnect; wcchatconnect]
        ]

        guibar

        guilist [
            guilist [
                guitext "Server: "
                guitext "Password: "
                guitext "Console Prefix: "
                guitext "Bind Chat To: "
            ]
            guibar
            guilist [
                guifield wcchatserver 32
                guifield wcchatpassword 32
                guifield wcchatprefix 10
                guifield wcchatbinds 5
            ]
        ]

        guicheckbox "Show Client Connects"  "wcchatshowconnects"
        guicheckbox "Show Connects To Game Servers" "wcchatshowservconnects"
        guicheckbox "Connect On Startup" "wcchatautoconnect"

        if ($wcchatisconnected) [
            guitab "Clients"

            process_gchat_clients
            render_gchat_clients
        ]
    ]
]

scriptevent "closegui" "guiname" [

    if (=s $guiname $gchat_gui_name) [
        delscriptvector gchat_client_names
        delscriptvector gchat_client_ids
        delscriptvector gchat_client_sec_connected
        delscriptvector gchat_client_country_names
        delscriptvector gchat_client_country_codes
        delscriptvector gchat_server_country_names
        delscriptvector gchat_server_country_codes
        delscriptvector gchat_server_names
        delscriptvector gchat_server_sec_connected
        delscriptvector gchat_server_addresses
    ]

]

globalchatgui = [ showgui $gchat_gui_name ]

] // end check wc
